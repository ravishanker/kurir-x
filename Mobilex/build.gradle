apply plugin: 'kotlin-multiplatform'
apply plugin: 'kotlinx-serialization'
apply plugin: 'org.jetbrains.kotlin.native.cocoapods'
apply plugin: 'co.touchlab.kotlinxcodesync'
apply from: '../version.gradle'

kotlin {

    jvm {

    }

    sourceSets {
        commonRelease {}

        commonDebug {}

        commonMain {
            if (rootProject.hasProperty("debug")) {
                dependsOn commonDebug
            } else {
                dependsOn commonRelease
            }

            dependencies {
                api "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlinVersion"
                api "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$coroutinesVersion"
                api "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:$serializationVersion"
                api "io.ktor:ktor-client-core:$ktorVersion"
                api "io.ktor:ktor-client-json:$ktorVersion"
                api "io.ktor:ktor-client-logging:$ktorVersion"
                api "io.ktor:ktor-client-serialization:$ktorVersion"
            }
        }

        commonTest {
            dependencies {
                api "org.jetbrains.kotlin:kotlin-test-common:$kotlinVersion"
            }
        }

        iOSMain {
            dependencies {
                api "org.jetbrains.kotlinx:kotlinx-coroutines-core-native:$coroutinesVersion"
                api "org.jetbrains.kotlinx:kotlinx-serialization-runtime-native:$serializationVersion"
                api "io.ktor:ktor-client-ios:$ktorVersion"
                api "io.ktor:ktor-client-json-native:$ktorVersion"
                api "io.ktor:ktor-client-logging-native:$ktorVersion"
                api "io.ktor:ktor-client-serialization-native:$ktorVersion"
            }
        }

        jvmMain {
            dependencies {
                api "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
                api "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serializationVersion"
                api "io.ktor:ktor-client-json-jvm:$ktorVersion"
                api "io.ktor:ktor-client-logging-jvm:$ktorVersion"
                api "io.ktor:ktor-client-serialization-jvm:$ktorVersion"
            }
        }

        jvmTest {
            dependencies {
                api "junit:junit:$junitVersion"
                api "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
                api "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
            }
        }
    }

    targets {
        def buildForDevice = project.findProperty("kotlin.native.cocoapods.target") == "ios_arm"
        if (buildForDevice) {
            iosArm64("iOS64")
            iosArm32("iOS32")

            def iOSMain = sourceSets.iOSMain
            sourceSets["iOS64Main"].dependsOn(iOSMain)
            sourceSets["iOS32Main"].dependsOn(iOSMain)
        } else {
            iosX64("iOS")
        }
    }

    cocoapods {
        summary = "MobileX module"
        homepage = "https://bitbucket.org/crazybeanstudio/mobilex"
    }
}

configurations {
    compileClasspath
}

xcode {
    projectPath = "../ios/Kurir.xcodeproj"
    target = "Kurir"
}

detekt {
    input = files(
            "src/commonMain/kotlin",
            "src/iOSMain/kotlin",
            "src/jvmMain/kotlin",
    )
}
